group = rootProject.group
String dependencyDir = group + '.dependencies'
version = rootProject.version
archivesBaseName = baseName

repositories {
    maven { url 'https://repo.incendo.org/content/repositories/snapshots/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly 'com.mojang:brigadier:1.0.17'
    compileOnly 'me.clip:placeholderapi:2.10.9'
    implementation 'me.lucko:commodore:1.10'
    implementation 'cloud.commandframework:cloud-paper:1.5.0-SNAPSHOT'
    implementation 'cloud.commandframework:cloud-annotations:1.5.0-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:1.8'
    implementation project(":Insights-API")
}

processResources {
    from('src/main/resources') {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        include 'plugin.yml'
        expand(version: project.version)
    }
}

shadowJar {
    exclude 'com/mojang/**'
    relocate 'org.bstats.bukkit', dependencyDir + '.bstats'
    relocate 'cloud.commandframework', dependencyDir + '.cloud'
    relocate 'io.leangen.geantyref', dependencyDir + '.typetoken'
    relocate 'me.lucko.commodore', dependencyDir + '.commodore'
}

publishing {
    publications {
        plugin(MavenPublication) {
            groupId = this.group
            artifactId = this.archivesBaseName
            version = this.version

            from components.java
        }
    }
}
